
#Область ПрограммныйИнтерфейс

Функция ПолучитьТаблицуЗначений(СтруктураСхемыЗапроса) Экспорт
	ТаблицаЗначений = СЗ_ПроцессорыСхемЗапросаСлужебный
		.ПолучитьРезультатЗапроса(СтруктураСхемыЗапроса)
		.Выгрузить();
	Возврат ПреобразоватьТаблицуЗначенийВМассивСтруктур(ТаблицаЗначений);
КонецФункции

Функция ПолучитьМассив(СтруктураСхемыЗапроса, ИмяКолонки) Экспорт
	Возврат СЗ_ПроцессорыСхемЗапросаСлужебный
		.ПолучитьРезультатЗапроса(СтруктураСхемыЗапроса)
		.Выгрузить()
		.ВыгрузитьКолонку(ИмяКолонки);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПреобразоватьТаблицуЗначенийВМассивСтруктур(ТаблицаЗначений)
	
	Массив = Новый Массив();
	
	СтруктураСтрокой = "";
	НужнаЗапятая 	 = Ложь;
	
	ОписаниеТаблицыЗначений = Новый ОписаниеТипов("ТаблицаЗначений");
	Для Каждого Колонка Из ТаблицаЗначений.Колонки Цикл
		Если Колонка.ТипЗначения = ОписаниеТаблицыЗначений Тогда
			Продолжить;
		КонецЕсли;
		Если НужнаЗапятая Тогда
			СтруктураСтрокой = СтруктураСтрокой + ",";
		КонецЕсли;
		
		СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
		НужнаЗапятая     = Истина;
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаЗначений Цикл
		НоваяСтрока = Новый Структура(СтруктураСтрокой);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		Массив.Добавить(НоваяСтрока);
	КонецЦикла;
	
	Возврат Массив;

КонецФункции

#КонецОбласти
