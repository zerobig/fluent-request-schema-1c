#Область ОписаниеПеременных

&НаКлиенте
Перем СхемаЗапроса;

#КонецОбласти

#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура УстановитьИсточникДанных(ИсточникДанных, Первые = -1, Различные = Ложь, Разрешенные = Ложь) Экспорт
	
	_ИсточникДанных = ИсточникДанных;
	Если ТипЗнч(ИсточникДанных) = Тип("ФормаКлиентскогоПриложения") Тогда
		_ИсточникДанных = ИсточникДанных.Получить();
	КонецЕсли;
	
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер.УстановитьИсточникДанных(СхемаЗапроса,
		_ИсточникДанных, Первые, Различные, Разрешенные);
		
КонецПроцедуры

&НаКлиенте
Функция Псевдоним(ИмяПсевдонима) Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПсевдонимИсточника(СхемаЗапроса, ИмяПсевдонима);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция ВТаблицуЗначений() Экспорт
	ТаблицаЗначений = СЗ_ПроцессорыСхемЗапросаСлужебныйВызовСервера
		.ПолучитьТаблицуЗначений(СхемаЗапроса);
	Возврат ТаблицаЗначений;
КонецФункции

&НаКлиенте
Функция ВМассив(ИмяКолонки) Экспорт
	Массив = СЗ_ПроцессорыСхемЗапросаСлужебныйВызовСервера
		.ПолучитьМассив(СхемаЗапроса, ИмяКолонки);
	Возврат Массив;
КонецФункции

&НаКлиенте
Функция Первые(Количество) Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПервые(СхемаЗапроса, Количество);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Различные() Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьРазличные(СхемаЗапроса);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Разрешенные() Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьРазрешенные(СхемаЗапроса);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Выбрать(Поля) Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПоляВыборки(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция ВыбратьВсе() Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьВыборВсехПолей(СхемаЗапроса);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Где(Предикат) Экспорт
	
	Если ТипЗнч(Предикат) = Тип("Структура") Тогда
		_Предикат = Предикат;
	ИначеЕсли ТипЗнч(Предикат) = Тип("ФормаКлиентскогоПриложения") Тогда
		_Предикат = Предикат.Получить();
	Иначе
		ВызватьИсключение "Неверный тип предиката в методе ""Где""";
	КонецЕсли;
	
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер.УстановитьУсловие(СхемаЗапроса, _Предикат);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция ЛевоеСоединение(ИсточникДанных, Предикат = Неопределено) Экспорт
	СоздатьСоединение("Левое", ИсточникДанных, Предикат);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция ПравоеСоединение(ИсточникДанных, Предикат = Неопределено) Экспорт
	СоздатьСоединение("Правое", ИсточникДанных, Предикат);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция ВнутреннееСоединение(ИсточникДанных, Предикат = Неопределено) Экспорт
	СоздатьСоединение("Внутреннее", ИсточникДанных, Предикат);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Сгруппировать(Поля) Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьГруппировку(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Упорядочить(Поля) Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПорядок(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Итоги(Поля) Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьИтоги(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Параметры(Параметры) Экспорт
	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер.УстановитьПараметры(СхемаЗапроса, Параметры);
	Возврат ЭтотОбъект;
КонецФункции

&НаКлиенте
Функция Получить() Экспорт
	
	Возврат СхемаЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьСоединение(ТипСоединения, ИсточникДанных, Предикат)
	
	// TODO: ИсточникДанных может быть структурой? Что делать в этом случае?
	Если ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
		_ИсточникДанных = ИсточникДанных;
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("ФормаКлиентскогоПриложения") Тогда
		_ИсточникДанных = ИсточникДанных.Получить();
	Иначе
		ВызватьИсключение "ЛевоеСоединение: неверный тип источника данных";
	КонецЕсли;
	
	Если ТипЗнч(Предикат) = Тип("Структура") Или Предикат = Неопределено Тогда
		_Предикат = Предикат;
	ИначеЕсли ТипЗнч(Предикат) = Тип("ФормаКлиентскогоПриложения") Тогда
		_Предикат = Предикат.Получить();
	Иначе
		ВызватьИсключение "ЛевоеСоединение: неверный тип предиката";
	КонецЕсли;

	СЗ_ПроцессорыСхемЗапросаСлужебныйКлиентСервер.ДобавитьСоединение(
		СхемаЗапроса, ТипСоединения, _ИсточникДанных, _Предикат);
	
КонецПроцедуры

#КонецОбласти
