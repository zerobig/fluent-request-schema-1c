#Область ОписаниеПеременных

Перем СхемаЗапроса;

#КонецОбласти

#Область ПрограммныйИнтерфейс

//
//
Процедура УстановитьИсточникДанных(ИсточникДанных, Первые = -1, Различные = Ложь, Разрешенные = Ложь) Экспорт
	
	Если ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
		_ИсточникДанных = ИсточникДанных;
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("ОбработкаОбъект.СхПроцессорСхемЗапроса") Тогда
		_ИсточникДанных = ИсточникДанных.Получить();
	Иначе
		ВызватьИсключение "Объединить: неверный тип источника данных";
	КонецЕсли;
	
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер.УстановитьИсточникДанных(СхемаЗапроса,
		_ИсточникДанных, Первые, Различные, Разрешенные);
	
КонецПроцедуры

//
//
Функция Псевдоним(ИмяПсевдонима) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПсевдонимИсточника(СхемаЗапроса, ИмяПсевдонима);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция ВТаблицуЗначений() Экспорт
	ТаблицаЗначений = СхПроцессорыСхемЗапросаСлужебный
		.ПолучитьРезультатЗапроса(СхемаЗапроса).Выгрузить();
	Возврат ТаблицаЗначений;
КонецФункции

//
//
Функция ВМассив(ИмяКолонки) Экспорт
	ТаблицаЗначений = СхПроцессорыСхемЗапросаСлужебный
		.ПолучитьРезультатЗапроса(СхемаЗапроса).Выгрузить();
	Возврат ТаблицаЗначений.ВыгрузитьКолонку(ИмяКолонки);
КонецФункции

//
//
Функция Первые(Количество) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПервые(СхемаЗапроса, Количество);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Различные() Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьРазличные(СхемаЗапроса);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Разрешенные() Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьРазрешенные(СхемаЗапроса);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Выбрать(Поля) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПоляВыборки(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция ВыбратьВсе() Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьВыборВсехПолей(СхемаЗапроса);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция ПараметрВиртуальнойТаблицы(Номер, Выражение) Экспорт
	
	Если ТипЗнч(Выражение) = Тип("Строка") Или ТипЗнч(Выражение) = Тип("Структура") Тогда
		_Выражение = Выражение;
	ИначеЕсли ТипЗнч(Выражение) = Тип("ОбработкаОбъект.СхПроцессорПредикатов") Тогда
		_Выражение = Выражение.Получить();
	Иначе
		ВызватьИсключение "Неверный тип выражения в методе ""ПараметрВиртуальнойТаблицы""";
	КонецЕсли;

	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПараметрВиртуальнойТаблицы(СхемаЗапроса, Номер, _Выражение);
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция Поместить(ИмяВременнойТаблицы) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьИмяВременнойТаблицы(СхемаЗапроса, ИмяВременнойТаблицы);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Где(Предикат) Экспорт
	
	Если ТипЗнч(Предикат) = Тип("Структура") Тогда
		_Предикат = Предикат;
	ИначеЕсли ТипЗнч(Предикат) = Тип("ОбработкаОбъект.СхПроцессорПредикатов") Тогда
		_Предикат = Предикат.Получить();
	Иначе
		ВызватьИсключение "Неверный тип предиката в методе ""Где""";
	КонецЕсли;
	
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер.УстановитьУсловие(СхемаЗапроса, _Предикат);
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция ЛевоеСоединение(ИсточникДанных, Предикат = Неопределено) Экспорт
	СоздатьСоединение("Левое", ИсточникДанных, Предикат);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция ПравоеСоединение(ИсточникДанных, Предикат = Неопределено) Экспорт
	СоздатьСоединение("Правое", ИсточникДанных, Предикат);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция ВнутреннееСоединение(ИсточникДанных, Предикат = Неопределено) Экспорт
	СоздатьСоединение("Внутреннее", ИсточникДанных, Предикат);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Сгруппировать(Поля) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьГруппировку(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция ВыборКогда(Предикат, ЗначениеИстина, ЗначениеЛожь, Псевдоним = "") Экспорт
	
	Если ТипЗнч(Предикат) = Тип("Структура") Тогда
		_Предикат = Предикат;
	ИначеЕсли ТипЗнч(Предикат) = Тип("ОбработкаОбъект.СхПроцессорПредикатов") Тогда
		_Предикат = Предикат.Получить();
	Иначе
		ВызватьИсключение "Неверный тип предиката в методе ""ВыборКогда""";
	КонецЕсли;

	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьВыборКогда(СхемаЗапроса, _Предикат, ЗначениеИстина, ЗначениеЛожь, Псевдоним);
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция Количество(Поле) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьКоличество(СхемаЗапроса, Поле);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Сумма(Поле) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьСумму(СхемаЗапроса, Поле);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Максимум(Поле) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьМаксимум(СхемаЗапроса, Поле);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Минимум(Поле) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьМинимум(СхемаЗапроса, Поле);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Выразить(Поле, ОписаниеТипа, Псевдоним = "") Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьВыразить(СхемаЗапроса, Поле, ОписаниеТипа, Псевдоним);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Упорядочить(Поля) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьПорядок(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Итоги(Поля) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьИтоги(СхемаЗапроса, Поля);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Объединить(ИсточникДанных) Экспорт
	
	Если ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
		_ИсточникДанных = ИсточникДанных;
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("ОбработкаОбъект.СхПроцессорСхемЗапроса") Тогда
		_ИсточникДанных = ИсточникДанных.Получить();
	Иначе
		ВызватьИсключение "Объединить: неверный тип источника данных";
	КонецЕсли;
	
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьОбъединение(СхемаЗапроса, _ИсточникДанных);
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция ОбъединитьВсе(ИсточникДанных) Экспорт
	
	Если ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
		_ИсточникДанных = ИсточникДанных;
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("ОбработкаОбъект.СхПроцессорСхемЗапроса") Тогда
		_ИсточникДанных = ИсточникДанных.Получить();
	Иначе
		ВызватьИсключение "Объединить: неверный тип источника данных";
	КонецЕсли;
	
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер
		.УстановитьОбъединениеВсе(СхемаЗапроса, _ИсточникДанных);
	Возврат ЭтотОбъект;
	
КонецФункции

//
//
Функция Параметры(Параметры) Экспорт
	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер.УстановитьПараметры(СхемаЗапроса, Параметры);
	Возврат ЭтотОбъект;
КонецФункции

//
//
Функция Получить() Экспорт
	
	Возврат СхемаЗапроса;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьСоединение(ТипСоединения, ИсточникДанных, Предикат)
	
	// TODO: ИсточникДанных может быть структурой? Что делать в этом случае?
	Если ТипЗнч(ИсточникДанных) = Тип("Строка") Тогда
		_ИсточникДанных = ИсточникДанных;
	ИначеЕсли ТипЗнч(ИсточникДанных) = Тип("ОбработкаОбъект.СхПроцессорСхемЗапроса") Тогда
		_ИсточникДанных = ИсточникДанных.Получить();
	Иначе
		ВызватьИсключение "ЛевоеСоединение: неверный тип источника данных";
	КонецЕсли;
	
	Если ТипЗнч(Предикат) = Тип("Структура") Или Предикат = Неопределено Тогда
		_Предикат = Предикат;
	ИначеЕсли ТипЗнч(Предикат) = Тип("ОбработкаОбъект.СхПроцессорПредикатов") Тогда
		_Предикат = Предикат.Получить();
	Иначе
		ВызватьИсключение "ЛевоеСоединение: неверный тип предиката";
	КонецЕсли;

	СхПроцессорыСхемЗапросаСлужебныйКлиентСервер.ДобавитьСоединение(
		СхемаЗапроса, ТипСоединения, _ИсточникДанных, _Предикат);
	
КонецПроцедуры

#КонецОбласти
